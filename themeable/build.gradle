apply plugin: 'java'
apply plugin: 'jacoco'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.android:android:4.1.1.4'

    compile presentationDependencies.supportAnnotation
    compile domainDependencies.okHttp
    compile domainDependencies.gson

    testCompile testDependencies.hamcrestCore
    testCompile testDependencies.hamcrestInt
    testCompile testDependencies.hamcrestLib
    testCompile testDependencies.junit
    testCompile testDependencies.mockito
    testCompile testDependencies.robolectric
}

jacoco {
    toolVersion = "0.7.1.201405082137"
    reportsDir = file("$buildDir/jacoco")
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}

test { finalizedBy jacocoTestReport }

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Themeable']
    pkg {
        repo = 'maven'
        name = 'Themeable'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/brettwold/Themeable.git'
        version {
            name = "${project.version}"
            desc = 'Themeable initial alpha release'
            released  = new Date()
            vcsTag = 'v0.1'
        }
    }
}

publishing {
    publications {
        Themeable(MavenPublication) {
            groupId 'com.brettwold'
            artifactId 'themeable'
            version "${project.version}"
            from components.java
        }
    }
}

uploadArchives {
    repositories {
        flatDir {
            dirs 'repos'
        }
    }
}